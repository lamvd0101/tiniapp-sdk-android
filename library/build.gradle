apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 30)
    buildToolsVersion safeExtGet("buildToolsVersion", "30.0.3")

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 21)
        targetSdkVersion safeExtGet("targetSdkVersion", 30)
    }
}

dependencies {
    api "com.facebook.infer.annotation:infer-annotation:0.11.2"
    api "javax.inject:javax.inject:1"
    //noinspection GradleDependency
    api "androidx.appcompat:appcompat:1.0.2"
    api "com.facebook.fresco:fresco:2.0.0"
    api "com.facebook.fresco:imagepipeline-okhttp3:2.0.0"
    api "com.facebook.soloader:soloader:0.6.0"
    api "com.google.code.findbugs:jsr305:3.0.2"
    api "com.squareup.okhttp3:okhttp:3.12.1"
    api "com.squareup.okhttp3:okhttp-urlconnection:3.12.1"
    api "com.squareup.okio:okio:1.15.0"

    String str = new File("${rootDir}/library/libs.txt").getText("UTF-8")
    List libs = str.tokenize("\n")
    libs.each {
        api it
    }

    api "com.facebook.conceal:conceal:1.1.3@aar"
    api "com.github.lamvd0101:tiniapp-sdk-android-libs:v1.1.0"
}
